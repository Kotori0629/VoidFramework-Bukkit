import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
}

group = 'com.github.julyss2019.bukkit'
version = '2.0.0'

dependencies {
    implementation project(':api')
    implementation project(':api-legacy')
    implementation 'me.lucko:jar-relocator:1.7'
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from 'src/main/resources/plugin.yml'
    filter(ReplaceTokens, beginToken: '${', endToken: '}', tokens: ["version": project.version])
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')

    final prefix = 'com.void01.bukkit.voidframework.core.libs.'

    relocate('org.objectweb', prefix + 'org.objectweb')
    relocate('me.lucko', prefix + 'me.lucko')

    relocate('kotlin', 'kotlin_1_9_10')
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)

            artifactId = 'void-framework-bukkit-core'
        }
    }
    repositories {
        mavenLocal()
    }
}