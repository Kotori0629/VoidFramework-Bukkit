import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'maven-publish'
}

group = 'com.github.julyss2019.bukkit'

dependencies {
    implementation project(':common')
    implementation project(':api')
    implementation project(':api-legacy')

    compileOnly 'org.redisson:redisson:3.27.2'
    compileOnly 'com.zaxxer:HikariCP:4.0.3'
    compileOnly 'org.mongodb:mongodb-driver-sync:4.11.1'
    compileOnly 'com.void01.bukkit.thirdparty:Vault:1.7.3-b131'
    compileOnly 'net.objecthunter:exp4j:0.4.8'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from 'src/main/resources/plugin.yml'
    filter(ReplaceTokens, beginToken: '${', endToken: '}', tokens: ["version": project.version, "gitHash": 'git rev-parse --short HEAD'.execute().text.trim()])
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')

    relocate('com.zaxxer.hikari', 'vf.com.zaxxer.hikari')
    relocate('kotlin', 'vf.kotlin')
    relocate('com.mongodb', 'vf.com.mongodb')
    relocate('org.bson', 'vf.org.bson')
    relocate('org.redisson', 'vf.org.redisson')
    relocate('net.objecthunter.exp4j', 'vf.net.objecthunter.exp4j')
}

tasks.named('build') {
    dependsOn shadowJar
}